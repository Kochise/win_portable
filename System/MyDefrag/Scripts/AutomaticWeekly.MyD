# MyDefrag v4.0 default script: Weekly
#
# Defragment and optimize all the disks. This script is designed for running
# automatically, it always processes all disks and ignores selections from the
# commandline and the script chooser, and it will automatically exit when finished.
#
# The script does the same tasks as the 'System Disk Weekly' script for the C: disk,
# and then the same tasks as the 'Data Disk Weekly' script for all the other disks.
# For more information see those scripts.
#
# This script is part of the standard MyDefrag distribution.




/* Automatically exit when the script has finished. This script is designed to
   be run via the task scheduler, so it must exit automatically. */
WhenFinished(exit)

/* If another instance of MyDefrag is already running then exit. */
OtherInstances(exit)



/* Write the header to the logfile. See the "Settings.MyD" file for the
   definition of the "LogHeader" string. */
WriteLogfile("MyDefrag.log","LogHeader")



/* Process the C: disk. */
VolumeSelect
  Name("c:")
VolumeActions

  /* Write the "before" statistics to the logfile. See the "Settings.MyD" file
     for the definition of the "LogBefore" string. */
  AppendLogfile("MyDefrag.log","LogBefore")

  /* Place files at 30% into the data on the disk. */
  MakeGap(RoundUp(VolumeUsed * 0.3,VolumeSize * 0.01), DoNotVacate)

  /* Zone 1: Place the MFT and some other special NTFS files. */
  FileSelect
    SelectNtfsSystemFiles(yes)
  FileActions
    PlaceNtfsSystemFiles(Ascending,MftSize * 0.1)
  FileEnd

  /* Zone 2: Directories. */
  FileSelect
    Directory(yes)
  FileActions
    SortByName(Ascending)
  FileEnd

  /* Place files at the beginning of the disk. */
  MakeGap(0)

  /* Zone 3: files used when booting, and a gap. */
  FileSelect
    ImportListFromBootOptimize()
  FileActions
    Defragment(Fast)
    Defragment()
    FastFill(WithShuffling)
    AddGap(RoundUp(ZoneEnd,VolumeFree * 0.01))
  FileEnd

  /* Zone 4: files used by the most used programs, and a gap. */
  FileSelect
    ImportListFromProgramHints("*.pf")
  FileActions
    Defragment(Fast)
    Defragment()
    FastFill(WithShuffling)
    AddGap(RoundUp(ZoneEnd,VolumeFree * 0.01))
  FileEnd

  /* Zone 5: regular files and a gap. */
  FileSelect
    not(
      Size(50000000,0)
      or (LastAccessEnabled(yes) and LastAccess(,1 month ago))
      or DirectoryName("$RECYCLE.BIN")
      or DirectoryName("RECYCLED")
      or DirectoryName("RECYCLER")
      or DirectoryName("$*")
      or DirectoryName("Downloaded Installations")
      or DirectoryName("Ehome")
      or DirectoryName("Fonts")
      or DirectoryName("Help")
      or DirectoryName("I386")
      or DirectoryName("IME")
      or DirectoryName("Installer")
      or DirectoryName("ServicePackFiles")
      or DirectoryName("SoftwareDistribution")
      or DirectoryName("Speech")
      or DirectoryName("Symbols")
      or DirectoryName("ie?updates")
      or DirectoryName("dllcache")
      or DirectoryName("Installshield Installation Information")
      or DirectoryName("System Volume Information")
      or DirectoryName("windows.old")
      or FileName("*.7z")
      or FileName("*.arj")
      or FileName("*.avi")
      or FileName("*.bak")
      or FileName("*.bup")
      or FileName("*.bz2")
      or FileName("*.cab")
      or FileName("*.chm")
      or FileName("*.dvr-ms")
      or FileName("*.gz")
      or FileName("*.ifo")
      or FileName("*.iso")
      or FileName("*.log")
      or FileName("*.lzh")
      or FileName("*.mp3")
      or FileName("*.msi")
      or FileName("*.old")
      or FileName("*.pdf")
      or FileName("*.rar")
      or FileName("*.rpm")
      or FileName("*.tar")
      or FileName("*.wav")
      or FileName("*.wmv")
      or FileName("*.vob")
      or FileName("*.z")
      or FileName("*.zip")
      )
  FileActions
    Defragment(Fast)
    Defragment()
    FastFill(WithShuffling)
    AddGap(RoundUp(ZoneEnd,VolumeFree * 0.01))
  FileEnd

  /* Zone 6: spacehog files (less important files that take up a lot of space). */
  FileSelect
    all
  FileActions
    Defragment(Fast)
    Defragment()
    FastFill(WithShuffling)
  FileEnd

  /* Write the "after" statistics to the logfile. See the "Settings.MyD" file
     for the definition of the "LogAfter" string. */
  AppendLogfile("MyDefrag.log","LogAfter")

VolumeEnd



/* Process all the other fixed, writable, mounted volumes. */
VolumeSelect
  Removable(no)
  and Writable(yes)
  and Mounted(yes)
VolumeActions

  /* Write the "before" statistics to the logfile. See the "Settings.MyD" file
     for the definition of the "LogBefore" string. */
  AppendLogfile("MyDefrag.log","LogBefore")

  /* Zone 1: Place the MFT and some other special NTFS files. */
  FileSelect
    SelectNtfsSystemFiles(yes)
  FileActions
    PlaceNtfsSystemFiles(Ascending,MftSize * 0.01)
  FileEnd

  /* Zone 2: Directories. */
  FileSelect
    Directory(yes)
  FileActions
    SortByName(Ascending)
  FileEnd

  MakeGap(RoundUp(ZoneBegin,VolumeSize * 0.001))

  /* Zone 3: all other files. */
  FileSelect
    all
  FileActions
    Defragment(Fast)
    Defragment()
    FastFill(WithShuffling)
  FileEnd

  /* Write the "after" statistics to the logfile. See the "Settings.MyD" file
     for the definition of the "LogAfter" string. */
  AppendLogfile("MyDefrag.log","LogAfter")

VolumeEnd



/* Write the footer to the logfile. See the "Settings.MyD" file for the
   definition of the "LogFooter" string. */
AppendLogfile("MyDefrag.log","LogFooter")
